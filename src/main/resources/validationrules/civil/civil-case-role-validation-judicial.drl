package validationrules.employment;
import uk.gov.hmcts.reform.roleassignment.domain.model.Assignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.RoleAssignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.Request;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.Classification
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.Status;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RequestType;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.GrantType;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RoleCategory;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RoleType
import uk.gov.hmcts.reform.roleassignment.domain.model.ExistingRoleAssignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.Case;
import function uk.gov.hmcts.reform.roleassignment.domain.service.common.ValidationModelService.logMsg;
import uk.gov.hmcts.reform.roleassignment.domain.model.FeatureFlag;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.FeatureFlagEnum;
import uk.gov.hmcts.reform.roleassignment.util.ValidationUtil;
import java.time.ZonedDateTime;
import java.time.temporal.ChronoUnit;
import uk.gov.hmcts.reform.roleassignment.domain.model.CaseAllocatorApproval;



/*
 * CIVIL "lead-judge" case validation rule.
 */
rule "civil_lead_judge_create_case_roles"
when
    $ca: CaseAllocatorApproval(
                 roleAssignment.status == Status.CREATE_REQUESTED,
                 roleAssignment.attributes["jurisdiction"].asText() == "CIVIL",
                 roleAssignment.roleName in ("lead-judge"))
         ExistingRoleAssignment(
                 actorId == $ca.getRoleAssignment().actorId,
                 roleType == RoleType.ORGANISATION,
                 roleName in ("leadership-judge"),
                 attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() == "CIVIL")
then
    $ca.getRoleAssignment().setStatus(Status.CREATE_APPROVED);
    $ca.getRoleAssignment().log("Stage 1 approved : civil_lead_judge_create_case_roles");
    update($ca.getRoleAssignment());
    logMsg("Rule : civil_lead_judge_create_case_roles");
end;

/*
 * CIVIL "allocated-judge" case validation rule.
 */
rule "civil_allocated-judge_create_case_roles"
when
    $ca: CaseAllocatorApproval(
                 roleAssignment.status == Status.CREATE_REQUESTED,
                 roleAssignment.attributes["jurisdiction"].asText() == "CIVIL",
                 roleAssignment.roleName in ("allocated-judge"))
         ExistingRoleAssignment(
                 actorId == $ca.getRoleAssignment().actorId,
                 roleType == RoleType.ORGANISATION,
                 roleName in ("judge", "fee-paid-judge", "circuit-judge", "district-judge", "deputy-district-judge", "recorder"),
                 attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() == "CIVIL")
then
    $ca.getRoleAssignment().setStatus(Status.CREATE_APPROVED);
    $ca.getRoleAssignment().log("Stage 1 approved : civil_allocated-judge_create_case_roles");
    update($ca.getRoleAssignment());
    logMsg("Rule : civil_allocated-judge_create_case_roles");
end;
