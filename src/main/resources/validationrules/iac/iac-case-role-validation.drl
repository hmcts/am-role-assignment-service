package validationrules.iac;
import uk.gov.hmcts.reform.roleassignment.domain.model.RoleAssignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.Request;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.Status;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RequestType;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.GrantType;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RoleCategory;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RoleType
import uk.gov.hmcts.reform.roleassignment.domain.model.ExistingRoleAssignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.Case;

/*
 * IAC case worker case roles can be created by any existing tribunal casworker
 * or senior tribunal case worker, and can also only be assigned to users with
 * one of those two roles (this includes self-assignment).
 */
rule "ia_caseworker_create_case_roles"
when
    $rq: Request(
             assignerId == authenticatedUserId)

    $ra: RoleAssignment(
              status == Status.CREATE_REQUESTED,
             roleType == RoleType.CASE,
             roleName == "tribunal-caseworker",
             attributes["caseId"] != null && $caseId : attributes["caseId"].asText())
         Case(
             id == $caseId,
             jurisdiction == "IA",
             caseTypeId == "Asylum")
         ExistingRoleAssignment(
             actorId == $ra.actorId,
             roleType == RoleType.ORGANISATION,
             roleName in ("tribunal-caseworker", "senior-tribunal-caseworker"),
             attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() == "IA")

         ExistingRoleAssignment(
             actorId == $rq.assignerId,
             roleType == RoleType.ORGANISATION,
             roleName in ("tribunal-caseworker", "senior-tribunal-caseworker"),
             attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() == "IA")

then
    $ra.setStatus(Status.CREATE_APPROVED);
    $ra.log("Stage 1 approved : ia_caseworker_create_case_roles");
    update($ra);
    System.out.println("Rule : ia_caseworker_create_case_roles");
end;

/*
 * IAC case worker case roles can be deleted by any existing tribunal casworker
 * or senior tribunal case worker.
 */
rule "ia_caseworker_delete_case_roles"
when
    $rq: Request(
             assignerId == authenticatedUserId)
    $ra: RoleAssignment(
             status == Status.DELETE_REQUESTED,
             roleType == RoleType.CASE,
             roleName == "tribunal-caseworker",
             attributes["caseId"] != null && $caseId : attributes["caseId"].asText())
         //we don't need to re-evaluate Case for deletion.
         Case(
             id == $caseId,
             jurisdiction == "IA",
             caseTypeId == "Asylum")
        ExistingRoleAssignment(
             actorId == $rq.assignerId,
             roleType == RoleType.ORGANISATION,
             roleName in ("tribunal-caseworker", "senior-tribunal-caseworker"),
             attributes["jurisdiction"]!=null,attributes["jurisdiction"].asText() == "IA")
              //validity of case role must be equal or within the validaty of existing role
              /*beginTime == null || beginTime <= $ra.beginTime,
              endTime == null || endTime >= $ra.endTime)*/
then
    $ra.setStatus(Status.DELETE_APPROVED);
    $ra.log("Delete approved : ia_caseworker_delete_case_roles");
    update($ra);
    System.out.println("Rule : ia_caseworker_delete_case_roles");
end;
