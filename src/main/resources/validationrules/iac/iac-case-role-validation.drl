package validationrules.iac;
import uk.gov.hmcts.reform.roleassignment.domain.model.RoleAssignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.Request;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.Status;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RequestType;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.GrantType;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RoleCategory;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RoleType
import uk.gov.hmcts.reform.roleassignment.domain.model.ExistingRoleAssignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.Case;

/*
 * IAC case worker case roles can be created by any existing tribunal casworker
 * or senior tribunal case worker, and can also only be assigned to users with
 * one of those two roles (this includes self-assignment).
 */
rule "ia_caseworker_create_case_roles"
when
    $rq: Request(
             requestType == RequestType.CREATE,
             //  shall we  remove this condition
             //shall we have condition on clientId
             assignerId == authenticatedUserId)

    $ra: RoleAssignment(
             status == Status.CREATED,
             roleType == RoleType.CASE,
             roleName == "tribunal-caseworker",
             attributes["caseId"] != null && $caseId : attributes["caseId"].asText())
          //do we need to check on case jurisdiction = "IA"
         Case(
             id == $caseId,
             caseTypeId == "Asylum")

         ExistingRoleAssignment(
             actorId == $ra.actorId,
             roleType == RoleType.ORGANISATION,
             roleName == "tribunal-caseworker" || roleName == "senior-tribunal-caseworker",
             attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() == "IA")
         ExistingRoleAssignment(
          // what if assignerId is != authenticatedUserId, shall we consider both with in operator
             actorId == $rq.assignerId,
             roleType == RoleType.ORGANISATION,
             // we can use here in operator
             roleName == "tribunal-caseworker" || roleName == "senior-tribunal-caseworker",
             attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() == "IA")
then
    $ra.setStatus(Status.STAGE_1_APPROVED);
    $ra.log("Stage 1 approved : ia_caseworker_create_case_roles");
    update($ra);
    System.out.println("Rule : ia_caseworker_create_case_roles");
end;

/*
 * IAC case worker case roles can be deleted by any existing tribunal casworker
 * or senior tribunal case worker.
 */
rule "ia_caseworker_delete_case_roles"
when
    $rq: Request(
             requestType == RequestType.DELETE,
             assignerId == authenticatedUserId)
    $ra: RoleAssignment(
             roleType == RoleType.CASE,
             roleName == "tribunal-caseworker",
             attributes["caseId"] != null && $caseId : attributes["caseId"].asText())
         Case(
             id == $caseId,
             caseTypeId == "Asylum")
         ExistingRoleAssignment(
            //we should authen
             actorId == $rq.assignerId,
             roleType == RoleType.ORGANISATION,
             roleName == "tribunal-caseworker" || roleName == "senior-tribunal-caseworker",
             attributes["jurisdiction"] == "IA")
then
    //why should we not approved this request here becuase pattern validation on existing records not making any sense.
    $ra.setStatus(Status.STAGE_1_APPROVED);
    $ra.log("Stage 1 approved : ia_caseworker_delete_case_roles");
    update($ra);
    System.out.println("Rule : ia_caseworker_delete_case_roles");
end;
