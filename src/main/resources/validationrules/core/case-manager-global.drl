package validationrules.core;
import uk.gov.hmcts.reform.roleassignment.domain.model.CaseAllocatorApproval;
import uk.gov.hmcts.reform.roleassignment.domain.model.ExistingRoleAssignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.Request;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.GrantType;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RoleType
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.Status;

/*
* All services: "admin-case-manager" and "ctsc-case-manager" case roles
*/
rule "create_case_manager_role"
when
    $f: FeatureFlag(status && flagName == FeatureFlagEnum.CASE_MANAGER_1_0.getValue())
    $rq: Request(assignerId == authenticatedUserId)
    $ca: CaseAllocatorApproval(
             roleAssignment.status == Status.CREATE_REQUESTED,
             roleAssignment.roleType == RoleType.CASE,
             roleAssignment.roleName in ("admin-case-manager", "ctsc-case-manager"),
             roleAssignment.grantType in (GrantType.SPECIFIC))
    ExistingRoleAssignment(
             actorId == $rq.assignerId,
             roleType == RoleType.ORGANISATION)
then
    $ca.getRoleAssignment().setStatus(Status.CREATE_APPROVED);
    $ca.getRoleAssignment().log("Stage 1 approved : create_case_manager_role");
    update($ca.getRoleAssignment());
    logMsg("Rule : create_case_manager_role");
end;

/*
* All services: "admin-case-manager" and "ctsc-case-manager" case roles deletion
*/
rule "delete_case_manager_role"
when
   $f: FeatureFlag(status && flagName == FeatureFlagEnum.CASE_MANAGER_1_0.getValue())
   $rq: Request(assignerId == authenticatedUserId)
   $ca: CaseAllocatorApproval(
            roleAssignment.actorId == $rq.authenticatedUserId,
            roleAssignment.status == Status.DELETE_REQUESTED,
            roleAssignment.roleType == RoleType.CASE,
            roleAssignment.roleName in ("admin-case-manager", "ctsc-case-manager"))
then
   $ca.getRoleAssignment().setStatus(Status.DELETE_APPROVED);
   $ca.getRoleAssignment().log("Stage 1 approved : delete_case_manager_role");
   update($ca.getRoleAssignment());
   logMsg("Rule : delete_case_manager_role");
end;
