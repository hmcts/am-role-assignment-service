package validationrules.core;
import uk.gov.hmcts.reform.roleassignment.domain.model.Assignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.RoleAssignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.Request;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.Status;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RequestType;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.GrantType;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RoleCategory;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.RoleType;
import uk.gov.hmcts.reform.roleassignment.domain.model.enums.Classification;
import uk.gov.hmcts.reform.roleassignment.domain.model.ExistingRoleAssignment;
import uk.gov.hmcts.reform.roleassignment.domain.model.CaseAllocatorApproval;
import uk.gov.hmcts.reform.roleassignment.domain.model.Case;
import function uk.gov.hmcts.reform.roleassignment.domain.service.common.ValidationModelService.logMsg;
import uk.gov.hmcts.reform.roleassignment.util.ValidationUtil;


rule "case_allocator_approve_create_case_role"
when
    // Only allow requests where the assigner is also the authenticated user
    $rq: Request(assignerId == authenticatedUserId)
    // When the request is to create:
    //   - a case role
    //   - with SPECIFIC or EXCLUDED grant type
    //   - with a case ID
    $ra: RoleAssignment(
             status == Status.CREATE_REQUESTED,
             roleType == RoleType.CASE,
             grantType in (GrantType.SPECIFIC, GrantType.EXCLUDED),
             attributes["caseId"] != null && $caseId : attributes["caseId"].asText())
    // And there is a case with that ID
    $c:  Case(id == $caseId)
    // And the *assigner* has an existing role assignment
    //   - with name case-allocator
    //   - of any role type (not restricted to organisational roles)
    //   - with a classification at least equal to the case's classification
    //   - with a jurisdiction which matches the case jurisdiction
    //   - with no case type or one which matches the case
    //   - with no case ID or one which matches the case
    //   - with no region or one which matches the case
    //   - with no base location or one which matches the case
    //   - with no allocated role category or one which matches the role being assigned
    //   - with no allocated role or one which matches the role being assigned
         ExistingRoleAssignment(
             actorId == $rq.assignerId,
             roleName == "case-allocator",
             $c.securityClassification != null && classification.isAtLeast($c.securityClassification),
             attributes["jurisdiction"] != null && attributes["jurisdiction"].asText() == $c.jurisdiction,
             attributes["caseType"] == null || attributes["caseType"].asText() == $c.caseTypeId,
             attributes["caseId"] == null || attributes["caseId"].asText() == $c.id,
             attributes["region"] == null || attributes["region"].asText() == $c.getRegion(),
             attributes["baseLocation"] == null || attributes["baseLocation"].asText() == $c.getBaseLocation(),
             attributes["allocatedRoleCategory"] == null ||
             ValidationUtil.csvContains($ra.roleCategory.toString(), attributes["allocatedRoleCategory"].asText()),
             attributes["allocatedRole"] == null ||
             ValidationUtil.csvContains($ra.roleName, attributes["allocatedRole"].asText()))
    //   - that CaseAllocatorApproval entity doesn't exist for the same assignment record, this is required  to avoid infinite loop.
         not CaseAllocatorApproval(roleAssignment.id == $ra.id)
then
    $ra.setAttribute("jurisdiction", $c.getJurisdiction());
    $ra.setAttribute("caseType", $c.getCaseTypeId());
    insert(new CaseAllocatorApproval($ra));
    $ra.log("Case Allocator approved : case_allocator_approve_create_case_role");
    update($ra);
    logMsg("Rule : case_allocator_approve_create_case_role");
end;

rule "case_allocator_approve_delete_case_role"
when
    // Only allow requests where the assigner is also the authenticated user
    $rq: Request(assignerId == authenticatedUserId)
    // When the request is to delete:
    //   - a case role
    //   - with SPECIFIC or EXCLUDED grant type
    //   - with a case ID
    $ra: RoleAssignment(
             status == Status.DELETE_REQUESTED,
             roleType == RoleType.CASE,
             grantType in (GrantType.SPECIFIC, GrantType.EXCLUDED),
             attributes["caseId"] != null && $caseId : attributes["caseId"].asText())
    // And the *assigner* has an existing role assignment
    //   - with name case-allocator
    //   - of any role type (not restricted to organisational roles)
    //   - with a jurisdiction which matches the case jurisdiction
    //   - with no case type or one which matches the case
    //   - with no case ID or one which matches the case
    //   - with no region or one which matches the case
    //   - with no base location or one which matches the case
    //   - with no allocated role category or one which matches the role being assigned
    //   - with no allocated role or one which matches the role being assigned
         ExistingRoleAssignment(
             actorId == $rq.assignerId,
             roleName == "case-allocator",
             attributes["jurisdiction"] == $ra.attributes["jurisdiction"],
             attributes["caseType"] == null || attributes["caseType"] == $ra.attributes["caseType"],
             attributes["caseId"] == null || attributes["caseId"] == $ra.attributes["caseId"],
             attributes["region"] == null || attributes["region"] == $ra.attributes["region"],
             attributes["baseLocation"] == null || attributes["baseLocation"] == $ra.attributes["baseLocation"],
             attributes["allocatedRoleCategory"] == null ||
             ValidationUtil.csvContains($ra.roleCategory.toString(), attributes["allocatedRoleCategory"].asText()),
             attributes["allocatedRole"] == null ||
             ValidationUtil.csvContains($ra.roleName, attributes["allocatedRole"].asText()))
    //   - that CaseAllocatorApproval entity doesn't exist for the same assignment record, this is required to avoid infinite loop.
         not CaseAllocatorApproval(roleAssignment.id == $ra.id)
then
    insert(new CaseAllocatorApproval($ra));
    $ra.log("Case Allocator approved : case_allocator_approve_delete_case_role");
    update($ra);
    logMsg("Rule : case_allocator_approve_delete_case_role");
end;

/*
 * The case allocator rules above will provide initial approval of exclusions.
 * The rules below implement the global requirement for case allocators to be able to create
 * and delete exclusions, and for users to be able to create conflicts of interest for themselves.
 */

rule "case_allocator_create_conflict_of_interest"
when
    // The case allocator rule has already succeeded for this role assignment,
    // which is creating a conflict of interest exclusion role.
    $ca: CaseAllocatorApproval(
        roleAssignment.status == Status.CREATE_REQUESTED,
        roleAssignment.roleName == "conflict-of-interest")
then
    // Set approved status
    $ca.getRoleAssignment().setStatus(Status.CREATE_APPROVED);
    $ca.getRoleAssignment().log("Stage 1 approved : case_allocator_create_conflict_of_interest");
    update($ca.getRoleAssignment());
    logMsg("Rule : case_allocator_create_conflict_of_interest");
end;

rule "case_allocator_delete_conflict_of_interest"
when
    $ca: CaseAllocatorApproval(
        roleAssignment.status == Status.DELETE_REQUESTED,
        roleAssignment.roleName == "conflict-of-interest")
then
    $ca.getRoleAssignment().setStatus(Status.DELETE_APPROVED);
    $ca.getRoleAssignment().log("Stage 1 approved : case_allocator_delete_conflict_of_interest");
    update($ca.getRoleAssignment());
    logMsg("Rule : case_allocator_delete_conflict_of_interest");
end;

/*
 * A user can create a conflict of interest case role for themselves.
 */

rule "self_create_conflict_of_interest"
when
    // Only allow requests where the assigner is also the authenticated user
    $rq: Request(assignerId == authenticatedUserId)
    // When the request is to create a role assignment:
    //   - for the requesting user
    //   - a case role
    //   - role name conflict-of-interest
    //   - with a case ID
    $ra: RoleAssignment(
             status == Status.CREATE_REQUESTED,
             actorId == $rq.authenticatedUserId,
             roleType == RoleType.CASE,
             roleName == "conflict-of-interest",
             attributes["caseId"] != null && $caseId : attributes["caseId"].asText())
    // And there is a case with that ID
    $c:  Case(id == $caseId)
then
    $ra.setAttribute("jurisdiction", $c.getJurisdiction());
    $ra.setAttribute("caseType", $c.getCaseTypeId());
    $ra.setStatus(Status.CREATE_APPROVED);
    $ra.log("Stage 1 approved : self_create_conflict_of_interest");
    update($ra);
    logMsg("Rule : self_create_conflict_of_interest");
end;
